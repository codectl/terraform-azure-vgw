module "naming" {
  source  = "codectl/naming/azure"
  version = "~> 0.1"

  suffix = ["demo", "dev"]
}

module "regions" {
  source  = "codectl/locations/azure"
  version = "~> 1.0"

  location = {
    primary = "westeurope"
  }
}

module "rg" {
  source  = "codectl/rg/azure"
  version = "~> 1.0"

  groups = {
    demo = {
      name     = module.naming.resource_group.name_unique
      location = module.regions.location.primary.name
    }
  }
}

module "network" {
  source  = "codectl/vnet/azure"
  version = "~> 1.0"

  vnet = {
    name                = module.naming.virtual_network.name
    address_space       = ["10.18.0.0/16"]
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name

    subnets = {
      sn1 = {
        name             = "gatewaysubnet"
        address_prefixes = ["10.18.1.0/24"]
      }
    }
  }
}

module "public_ip" {
  source  = "codectl/pip/azure"
  version = "~> 1.0"

  resource_group_name = module.rg.groups.demo.name
  location            = module.rg.groups.demo.location

  public_ips = {
    pip1 = {
      name              = "${module.naming.public_ip.name}-vgw"
      allocation_method = "Static"
      sku               = "Standard"
      zones             = ["1", "2", "3"]
    }
  }
}

module "nat" {
  source  = "codectl/vgw/azure//modules/nat-rules"
  version = "~> 1.0"

  resource_group_name = module.rg.groups.demo.name
  rules               = local.rules

  virtual_network_gateway_id = module.vgw.gateway.id
}

module "lgw" {
  source  = "codectl/vgw/azure//modules/local-gateway"
  version = "~> 1.0"

  resource_group_name = module.rg.groups.demo.name
  location            = module.rg.groups.demo.location

  virtual_network_gateway_id = module.vgw.gateway.id

  local_gateways = {
    adrz = {
      gateway_address = "10.0.0.1"
      address_space   = ["1.2.3.4/32"]
      connection = {
        shared_key = "ie9p8y32r78eho'pmkl/dns3289ry"
        egress_nat_rule_ids = [
          module.nat.rules.rule1.id,
          module.nat.rules.rule2.id
        ]
      }
    }
  }
}

module "vgw" {
  source  = "codectl/vgw/azure"
  version = "~> 1.0"

  gateway = {
    name                = module.naming.virtual_network_gateway.name
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    sku                 = "VpnGw1"
    type                = "Vpn"

    ip_configurations = {
      default = {
        name                 = "vnetgatewayconfig"
        subnet_id            = module.network.subnets.sn1.id
        public_ip_address_id = module.public_ip.public_ips.pip1.id
      }
    }
  }
}
